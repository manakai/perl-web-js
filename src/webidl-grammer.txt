* Definitions

_SpecDirective
_ObsoleteDirective
_Definition*=definitions[]

* _SpecDirective

## Non-standard
'[' other:'*' identifier:'spec' '=' identifier="spec" other:'*' ']'
?

* _ObsoleteDirective

## Non-standard
'[' other:'*' identifier:'obsolete'=obsolete? other:'*' ']'
?

* _Definition

+pos
ExtendedAttributeList Definition

* Definition

CallbackOrInterface
|
Partial
|
Dictionary
|
Enum
|
Typedef
|
ImplementsStatement

## Not in spec
|
_Class

* CallbackOrInterface

_Callback
|
Interface

* _Callback

#'callback' CallbackRestOrInterface

'callback'="definition_type" CallbackRest
|
'callback'=callback? Interface

* CallbackRestOrInterface

CallbackRest
|
Interface

* _Name

_IDInSpecDirective
identifier=_"name"

* _IDInSpecDirective

## Non-standard
'[' other:'*' identifier:'id' '=' string="id" other:'*' ']'
?

* _IDInSpecDirectiveIgnored

## Non-standard
'[' other:'*' identifier:'id' '=' string other:'*' ']'
?

* Interface

'interface'="definition_type" _Name Inheritance '{' InterfaceMembers '}' ';'

* _Class

## Not in spec
## <http://dom.spec.whatwg.org/#elements>
## <https://www.w3.org/Bugs/Public/show_bug.cgi?id=23225>
'class'="definition_type" _Name _ClassInheritance '{' InterfaceMembers '}' ';'

* Partial

'partial'=partial? PartialDefinition

* PartialDefinition

PartialInterface
|
PartialDictionary

* PartialInterface

'interface'="definition_type" _Name '{' InterfaceMembers '}' ';'

* InterfaceMembers

_InterfaceMember*=members[]

* _InterfaceMember

+pos
ExtendedAttributeList InterfaceMember

* InterfaceMember

Const
|
Operation
|
Stringifier
|
StaticMember
|
Iterable
|
ReadonlyMember
|
ReadWriteAttribute
|
ReadWriteMaplike
|
ReadWriteSetlike

* Dictionary

'dictionary'="definition_type" _Name Inheritance '{' DictionaryMembers '}' ';'

* DictionaryMembers

_DictionaryMember*=members[]

* _DictionaryMember

+pos
+member_type="dictionary_member"
DictionaryMember

* DictionaryMember

ExtendedAttributeList 'required'=required? TypeWithExtendedAttributes _Name
Default ';'
|
ExtendedAttributeList Type _Name Default ';'

* PartialDictionary

'dictionary'="definition_type" _Name '{' DictionaryMembers '}' ';'

* Default

'=' DefaultValue
?

* DefaultValue

ConstValue
|
_IDInSpecDirectiveIgnored
string="value_string"
|
'[' ']'=value_empty_sequence?

* Inheritance

':' _SuperName
?

* _ClassInheritance

## Not in spec
'extends' _SuperName
?

* _SuperName

_IDInSpecDirectiveIgnored
identifier="super_name"

* Enum

'enum'="definition_type" _Name '{' EnumValueList '}' ';'

* EnumValueList

_EnumItem=value_items[] EnumValueListComma

* _EnumItem

_IDInSpecDirective
string="value_string"

* EnumValueListComma

',' EnumValueListString
?

* EnumValueListString

_EnumItem=value_items[] EnumValueListComma
?

* CallbackRest

_Name '=' ReturnType '(' ArgumentList ')' ';'

* Typedef

'typedef'="definition_type" TypeWithExtendedAttributes _Name ';'

* ImplementsStatement

identifier="name" 'implements'="definition_type" _SuperName ';'

* Const

'const'="member_type" ConstType _Name '=' ConstValue ';'

* ConstValue

BooleanLiteral
|
FloatLiteral
|
integer="value_integer"
|
'null'="value"

* BooleanLiteral

'true'="value"
|
'false'="value"

* FloatLiteral

float="value_float"
|
'-Infinity'="value"
|
'Infinity'="value"
|
'NaN'="value"

* Stringifier

_IDInSpecDirective
'stringifier'=stringifier? StringifierRest

* StringifierRest

AttributeRest
|
ReturnType OperationRest
|
_StringifierEnd

* _StringifierEnd

+member_type="operation"
+type="DOMString"
+arguments[]
';'

* StaticMember

'static'=static? StaticMemberRest

* StaticMemberRest

AttributeRest
|
ReturnType OperationRest

* ReadonlyMember

'readonly'=readonly? ReadonlyMemberRest

* ReadonlyMemberRest

AttributeRest
|
ReadWriteMaplike
|
ReadWriteSetlike

* ReadWriteAttribute

'inherit'=inherit? ReadOnly AttributeRest
|
AttributeRest

* AttributeRest

ReadOnly 'attribute'="member_type" TypeWithExtendedAttributes AttributeName ';'

* AttributeName

_Name
|
_AttributeNameKeyword

* _AttributeNameKeyword

_IDInSpecDirective
AttributeNameKeyword

* Inherit

'inherit'=inherit?
?

* ReadOnly

'readonly'=readonly?
?

* Operation

ReturnType OperationRest
|
SpecialOperation

* SpecialOperation

Special Specials ReturnType OperationRest

* Specials

Special Specials
?

* Special

'getter'=getter?
|
'setter'=setter?
|
'deleter'=deleter?
|

## Non-standard : _IDInSpecDirective
'[' other:'*' identifier:'id' '=' string="getter_id" other:'*' ']'
'getter'=getter?
|
'[' other:'*' identifier:'id' '=' string="setter_id" other:'*' ']'
'setter'=setter?
|
'[' other:'*' identifier:'id' '=' string="deleter_id" other:'*' ']'
'deleter'=deleter?

* OperationRest

+member_type="operation"
OptionalIdentifier '(' ArgumentList ')' ';'

* OptionalIdentifier

_Name
?

* ArgumentList

+arguments[]
Argument=arguments[] Arguments
?

* Arguments

',' Argument=arguments[] Arguments
?

* Argument

+pos
_Argument

* _Argument

ExtendedAttributeList 'optional'=optional? TypeWithExtendedAttributes
ArgumentName Default

|

## Error handling (not in spec)
Type ArgumentName '=' DefaultValue

|
ExtendedAttributeList Type Ellipsis ArgumentName

* ArgumentName

_ArgumentNameKeyword
|
_Name

* _ArgumentNameKeyword

_IDInSpecDirective
ArgumentNameKeyword

* Ellipsis

'...'=variadic?
?

* Iterable

'iterable'="member_type" '<' TypeWithExtendedAttributes=type1
OptionalType '>' ';'

* OptionalType

',' TypeWithExtendedAttributes=type2
?

* ReadWriteMaplike

MaplikeRest

* ReadWriteSetlike

SetlikeRest

* MaplikeRest

'maplike'="member_type" '<' TypeWithExtendedAttributes=type1 ',' 
TypeWithExtendedAttributes=type2 '>' ';'

* SetlikeRest

'setlike'="member_type" '<' TypeWithExtendedAttributes=type1 '>' ';'

* ExtendedAttributeList

_ExtendedAttributeList
?

* _ExtendedAttributeList

'[' ExtendedAttribute=extended_attributes[] ExtendedAttributes ']'

* ExtendedAttributes

',' ExtendedAttribute=extended_attributes[] ExtendedAttributes
?

* ExtendedAttribute

+pos

#'('="item_type" ExtendedAttributeInner ')' ExtendedAttributeRest
#|
#'['="item_type" ExtendedAttributeInner ']' ExtendedAttributeRest
#|
#'{'="item_type" ExtendedAttributeInner '}' ExtendedAttributeRest
#|
#Other ExtendedAttributeRest

ExtendedAttributeArgList
|
ExtendedAttributeNamedArgList
|
ExtendedAttributeIdentList
|
ExtendedAttributeNoArgs
|
ExtendedAttributeIdent

* ExtendedAttributeRest

ExtendedAttribute=extended_attributes[]
?

* ExtendedAttributeInner

'('="item_type" ExtendedAttributeInner ')' ExtendedAttributeInner
|
'['="item_type" ExtendedAttributeInner ']' ExtendedAttributeInner
|
'{'="item_type" ExtendedAttributeInner '}' ExtendedAttributeInner
|
OtherOrComma ExtendedAttributeInner
?

* TypeWithExtendedAttributes

Type
|
_TypeWithExtendedAttributes=type

* _TypeWithExtendedAttributes

_ExtendedAttributeList SingleType
|
_ExtendedAttributeList UnionType Null

* Type

SingleType
|
UnionType Null

* SingleType

NonAnyType
|
'any'="type"

* UnionType

'(' UnionMemberType=type_union[] 'or' UnionMemberType=type_union[] UnionMemberTypes ')'

* UnionMemberType

ExtendedAttributeList NonAnyType
|
UnionType Null

* UnionMemberTypes

'or' UnionMemberType=type_union[] UnionMemberTypes
?

* NonAnyType

PrimitiveType Null
|
StringType Null
|
_IDInSpecDirectiveIgnored
identifier="type_name" Null
|
_SequenceType=type_parameterized Null
|
'object'="type" Null
|
'Date'="type" Null
|
'RegExp'="type" Null
|
'Error'="type" Null
|
'DOMException'="type" Null
|
BufferRelatedType Null
|
PromiseType=type_parameterized
|
_FrozenArrayType=type_parameterized Null
|
RecordType=type_parameterized Null

* _SequenceType

'sequence'="type_outer" '<' TypeWithExtendedAttributes '>'

* StringType

'ByteString'="type"
|
'DOMString'="type"
|
'USVString'="type"

* PromiseType

'Promise'="type_outer" '<' ReturnType '>'

* _FrozenArrayType

'FrozenArray'="type_outer" '<' TypeWithExtendedAttributes '>'

* RecordType

'record'="type_outer" '<' _RecordKeyType=type_key ',' TypeWithExtendedAttributes '>'

* _RecordKeyType

StringType

* BufferRelatedType

'ArrayBuffer'="type"
|
'DataView'="type"
|
'Int8Array'="type"
|
'Int16Array'="type"
|
'Int32Array'="type"
|
'Uint8Array'="type"
|
'Uint16Array'="type"
|
'Uint32Array'="type"
|
'Uint8ClampedArray'="type"
|
'Float32Array'="type"
|
'Float64Array'="type"

* ConstType

PrimitiveType Null
|
identifier="type_name" Null

* PrimitiveType

UnsignedIntegerType
|
UnrestrictedFloatType
|
'boolean'="type"
|
'byte'="type"
|
'octet'="type"

* UnrestrictedFloatType

#'unrestricted' FloatType
_UnrestrictedFloat
|
_UnrestrictedDouble

|
FloatType

* FloatType

'float'="type"
|
'double'="type"

* _UnrestrictedFloat

+type="unrestricted float"
'unrestricted' 'float'

* _UnrestrictedDouble

+type="unrestricted double"
'unrestricted' 'double'

* UnsignedIntegerType

#'unsigned' IntegerType
_UnsignedShort
|
_UnsignedLongLong
|
_UnsignedLong

|
IntegerType

* IntegerType

'short'="type"
|
#'long'="type" OptionalLong
_LongLong
|
_Long

* OptionalLong

'long'
?

* _Long

'long'="type"

* _LongLong

+type="long long"
'long' 'long'

* _UnsignedShort

+type="unsigned short"
'unsigned' 'short'

* _UnsignedLong

+type="unsigned long"
'unsigned' 'long'

* _UnsignedLongLong

+type="unsigned long long"
'unsigned' 'long' 'long'

* Null

'?'=type_nullable?
?

* ReturnType

Type
|
'void'="type"

* IdentifierList

identifier="value_names"[]!'='!'(' Identifiers

* Identifiers

',' identifier="value_names"[]!'='!'(' Identifiers
?

* ExtendedAttributeNoArgs

_IDInSpecDirective
identifier="name"!'('!'='

* ExtendedAttributeArgList

_IDInSpecDirective
identifier="name" '(' ArgumentList ')'

* ExtendedAttributeIdent

_IDInSpecDirective
identifier="name" '='
_IDInSpecDirective
identifier="value_names"[]!'='!'('

* ExtendedAttributeIdentList

_IDInSpecDirective
identifier="name" '=' '(' IdentifierList ')'

* ExtendedAttributeNamedArgList

_IDInSpecDirective
identifier="name" '='
_IDInSpecDirective
identifier="value_names"[] '(' ArgumentList ')'
