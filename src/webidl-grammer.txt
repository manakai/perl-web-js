* Definitions

_SpecDirective
_ObsoleteDirective
_Definition*=definitions[]

* _SpecDirective

## Non-standard
'[' other:'*' identifier:'spec' '=' identifier="spec" other:'*' ']'
?

* _ObsoleteDirective

## Non-standard
'[' other:'*' identifier:'obsolete'=obsolete? other:'*' ']'
?

* _Definition

+pos
ExtendedAttributeList Definition

* Definition

CallbackOrInterface
|
Partial
|
Dictionary
|
Exception
|
Enum
|
Typedef
|
ImplementsStatement

## Not in spec
|
_Class

* CallbackOrInterface

_Callback
|
Interface

* _Callback

#'callback' CallbackRestOrInterface

'callback'="definition_type" CallbackRest
|
'callback'=callback? Interface

* CallbackRestOrInterface

CallbackRest
|
Interface

* _Name

_IDInSpecDirective
identifier=_"name"

* _IDInSpecDirective

## Non-standard
'[' other:'*' identifier:'id' '=' string="id" other:'*' ']'
?

* _IDInSpecDirectiveIgnored

## Non-standard
'[' other:'*' identifier:'id' '=' string other:'*' ']'
?

* Interface

'interface'="definition_type" _Name Inheritance '{' InterfaceMembers '}' ';'

* _Class

## Not in spec
## <http://dom.spec.whatwg.org/#elements>
## <https://www.w3.org/Bugs/Public/show_bug.cgi?id=23225>
'class'="definition_type" _Name _ClassInheritance '{' InterfaceMembers '}' ';'

* Partial

'partial'=partial? PartialDefinition

* PartialDefinition

PartialInterface
|
PartialDictionary

* PartialInterface

'interface'="definition_type" _Name '{' InterfaceMembers '}' ';'

* InterfaceMembers

_InterfaceMember*=members[]

* _InterfaceMember

+pos
ExtendedAttributeList InterfaceMember

* InterfaceMember

Const
|
AttributeOrOperationOrIterator

* Dictionary

'dictionary'="definition_type" _Name Inheritance '{' DictionaryMembers '}' ';'

* DictionaryMembers

_DictionaryMember*=members[]

* _DictionaryMember

+pos
+member_type="dictionary_member"
ExtendedAttributeList DictionaryMember

* DictionaryMember

Type _Name Default ';'

* PartialDictionary

'dictionary'="definition_type" _Name '{' DictionaryMembers '}' ';'

* Default

'=' DefaultValue
?

* DefaultValue

ConstValue
|
string="value_string"

* Exception

'exception'="definition_type" _Name Inheritance '{' ExceptionMembers '}' ';'

* ExceptionMembers

_ExceptionMember*=members[]

* _ExceptionMember

+pos
ExtendedAttributeList ExceptionMember

* Inheritance

':' identifier="super_name"
?

* _ClassInheritance

## Not in spec
'extends' identifier="super_name"
?

* Enum

'enum'="definition_type" _Name '{' EnumValueList '}' ';'

* EnumValueList

_EnumItem=value_items[] EnumValueListComma

* _EnumItem

_IDInSpecDirective
string="value_string"

* EnumValueListComma

',' EnumValueListString
?

* EnumValueListString

_EnumItem=value_items[] EnumValueListComma
?

* CallbackRest

_Name '=' ReturnType '(' ArgumentList ')' ';'

* Typedef

'typedef'="definition_type" Type _Name ';'

* ImplementsStatement

identifier="name" 'implements'="definition_type" identifier="super_name" ';'

* Const

'const'="member_type" ConstType _Name '=' ConstValue ';'

* ConstValue

BooleanLiteral
|
FloatLiteral
|
integer="value_integer"
|
'null'="value"

* BooleanLiteral

'true'="value"
|
'false'="value"

* FloatLiteral

float="value_float"
|
'-Infinity'="value"
|
'Infinity'="value"
|
'NaN'="value"

* AttributeOrOperationOrIterator

Serializer
|
Stringifier
|
StaticMember
|
Attribute
|
OperationOrIterator

* Serializer

#'serializer'=serializer? SerializerRest
_SerializerOperation
|
_Serializer

* SerializerRest

## 'ReturnType' is not in spec grammer but required in prose
ReturnType
OperationRest
|
'=' SerializationPattern
|
_SerializerEnd

* _SerializerOperation

'serializer'=serializer? ReturnType OperationRest

* _Serializer

_IDInSpecDirective
'serializer'="member_type" '=' SerializationPattern ';'
|
_IDInSpecDirective
'serializer'="member_type" ';'

## ';' is missing in spec

* SerializationPattern

'{' SerializationPatternMap=value_map '}'
|
'[' SerializationPatternList=value_list ']'
|
identifier="value_name"

* SerializationPatternMap

+value_names[]

'getter'=getter?
|

## Not in spec grammer but defined in prose
'attribute'=attribute?
|
'inherit'=inherit? ',' 'attribute'=attribute?

|
'inherit'=inherit? Identifiers
|
identifier="value_names"[] Identifiers

?

* SerializationPatternList

+value_names[]

'getter'=getter?
|
identifier="value_names"[] Identifiers
?

* Stringifier

_IDInSpecDirective
'stringifier'=stringifier? StringifierRest

* StringifierRest

AttributeRest
|
ReturnType OperationRest
|
_StringifierEnd

* _StringifierEnd

+member_type="operation"
+type="DOMString"
+arguments[]
';'

* StaticMember

'static'=static? StaticMemberRest

* StaticMemberRest

AttributeRest
|
ReturnType OperationRest

* Attribute

Inherit AttributeRest

* AttributeRest

ReadOnly 'attribute'="member_type" Type _Name ';'

* Inherit

'inherit'=inherit?
?

* ReadOnly

'readonly'=readonly?
?

* OperationOrIterator

ReturnType OperationOrIteratorRest
|
SpecialOperation

* SpecialOperation

_IDInSpecDirective
Special Specials ReturnType OperationRest

* Specials

_IDInSpecDirectiveIgnored
Special Specials
?

* Special

'getter'=getter?
|
'setter'=setter?
|
'creator'=creator?
|
'deleter'=deleter?
|
'legacycaller'=legacycaller?

* OperationOrIteratorRest

IteratorRest
|
OperationRest

* IteratorRest

#'iterator'="member_type" OperationIteratorInterfaceOrObject ';'

'iterator'="member_type" OptionalIteratorInterface ';'
|
_IteratorObject

* OperationIteratorInterfaceOrObject

OptionalIteratorInterface
|
'object'=object?

* _IteratorObject

+member_type="iterator_object"
'iterator' 'object' ';'

* OptionalIteratorInterface

'=' identifier="value_name"
?

* OperationRest

+member_type="operation"
OptionalIdentifier '(' ArgumentList ')' ';'

* OptionalIdentifier

_Name
?

* ArgumentList

+arguments[]
Argument=arguments[] Arguments
?

* Arguments

',' Argument=arguments[] Arguments
?

* Argument

+pos
ExtendedAttributeList OptionalOrRequiredArgument

* OptionalOrRequiredArgument

'optional'=optional? Type ArgumentName Default
|

## Error handling (not in spec)
Type ArgumentName '=' DefaultValue

|
Type Ellipsis ArgumentName

* ArgumentName

ArgumentNameKeyword
|
_Name

* Ellipsis

'...'=variadic?
?

* ExceptionMember

Const
|
ExceptionField

* ExceptionField

+member_type="field"
Type _Name ';'

* ExtendedAttributeList

'[' ExtendedAttribute=extended_attributes[] ExtendedAttributes ']'
?

* ExtendedAttributes

',' ExtendedAttribute=extended_attributes[] ExtendedAttributes
?

* ExtendedAttribute

+pos

#'('="item_type" ExtendedAttributeInner ')' ExtendedAttributeRest
#|
#'['="item_type" ExtendedAttributeInner ']' ExtendedAttributeRest
#|
#'{'="item_type" ExtendedAttributeInner '}' ExtendedAttributeRest
#|
#Other ExtendedAttributeRest

ExtendedAttributeArgList
|
ExtendedAttributeTypePair
|

#ExtendedAttributeIdent
identifier:'PutForwards'="name" '=' identifier="value_names"[]!'='!'('

|
ExtendedAttributeNamedArgList
|
ExtendedAttributeIdentList
|
ExtendedAttributeNoArgs
|

#ExtendedAttributeIdent
identifier="name" '='
_IDInSpecDirective
identifier="value_names"[]!'='!'('

* ExtendedAttributeRest

ExtendedAttribute=extended_attributes[]
?

* ExtendedAttributeInner

'('="item_type" ExtendedAttributeInner ')' ExtendedAttributeInner
|
'['="item_type" ExtendedAttributeInner ']' ExtendedAttributeInner
|
'{'="item_type" ExtendedAttributeInner '}' ExtendedAttributeInner
|
OtherOrComma ExtendedAttributeInner
?

* Type

SingleType
|
UnionType TypeSuffix

* SingleType

NonAnyType
|
'any'="type" TypeSuffixStartingWithArray

* UnionType

'(' UnionMemberType=type_union[] 'or' UnionMemberType=type_union[] UnionMemberTypes ')'

* UnionMemberType

NonAnyType
|
UnionType TypeSuffix
|
'any'="type" _TypeArray=type_array

* UnionMemberTypes

'or' UnionMemberType=type_union[] UnionMemberTypes
?

* NonAnyType

PrimitiveType TypeSuffix
|
'ByteString'="type" TypeSuffix
|
'DOMString'="type" TypeSuffix
|
_IDInSpecDirective
identifier="type_name" TypeSuffix
|
_SequenceType=type_parameterized Null
|
'object'="type" TypeSuffix
|
'Date'="type" TypeSuffix
|
'RegExp'="type" TypeSuffix

|
## Not in spec
_PromiseType=type_parameterized Null
|
'Promise'="type" TypeSuffix

* _SequenceType

'sequence'="type_outer" '<' Type '>'

* _PromiseType

'Promise'="type_outer" '<' Type '>'

* ConstType

PrimitiveType Null
|
identifier="type_name" Null

* PrimitiveType

UnsignedIntegerType
|
UnrestrictedFloatType
|
'boolean'="type"
|
'byte'="type"
|
'octet'="type"

* UnrestrictedFloatType

#'unrestricted' FloatType
_UnrestrictedFloat
|
_UnrestrictedDouble

|
FloatType

* FloatType

'float'="type"
|
'double'="type"

* _UnrestrictedFloat

+type="unrestricted float"
'unrestricted' 'float'

* _UnrestrictedDouble

+type="unrestricted double"
'unrestricted' 'double'

* UnsignedIntegerType

#'unsigned' IntegerType
_UnsignedShort
|
_UnsignedLongLong
|
_UnsignedLong

|
IntegerType

* IntegerType

'short'="type"
|
#'long'="type" OptionalLong
_LongLong
|
_Long

* OptionalLong

'long'
?

* _Long

'long'="type"

* _LongLong

+type="long long"
'long' 'long'

* _UnsignedShort

+type="unsigned short"
'unsigned' 'short'

* _UnsignedLong

+type="unsigned long"
'unsigned' 'long'

* _UnsignedLongLong

+type="unsigned long long"
'unsigned' 'long' 'long'

* TypeSuffix

_TypeArray=type_array
|
'?'=type_nullable? TypeSuffixStartingWithArray
?

* TypeSuffixStartingWithArray

_TypeArray=type_array
?

* _TypeArray

'[' ']' TypeSuffix

* Null

'?'=type_nullable?
?

* ReturnType

Type
|
'void'="type"

* IdentifierList

identifier="value_names"[]!'='!'(' Identifiers

* Identifiers

',' identifier="value_names"[]!'='!'(' Identifiers
?

* ExtendedAttributeNoArgs

_IDInSpecDirective
identifier="name"!'('!'='

* ExtendedAttributeArgList

_IDInSpecDirective
identifier="name" '(' ArgumentList ')'

* ExtendedAttributeIdent

## Unused (-> ExtendedAttributeIdentList)
#_IDInSpecDirective
identifier="name" '='
_IDInSpecDirective
identifier="value_names"[]!'='!'('

* ExtendedAttributeIdentList

_IDInSpecDirective
identifier="name" '=' IdentifierList!'='!'('

* ExtendedAttributeNamedArgList

#_IDInSpecDirective
identifier="name" '='
_IDInSpecDirective
identifier="value_names"[] '(' ArgumentList ')'

* ExtendedAttributeTypePair

_IDInSpecDirective
identifier="name" '(' Type=value_types[] ',' Type=value_types[] ')'
