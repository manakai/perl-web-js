* Definitions

_Definition*=definitions[]

* _Definition

+pos
ExtendedAttributeList Definition

* Definition

CallbackOrInterface
|
Partial
|
Dictionary
|
Exception
|
Enum
|
Typedef
|
ImplementsStatement

## Not in spec
|
_Class

* CallbackOrInterface

_Callback
|
Interface

* _Callback

#'callback' CallbackRestOrInterface

'callback'="definition_type" CallbackRest
|
'callback'=callback? Interface

* CallbackRestOrInterface

CallbackRest
|
Interface

* Interface

'interface'="definition_type" identifier=_"name" Inheritance '{' InterfaceMembers '}' ';'

* _Class

## Not in spec
## <http://dom.spec.whatwg.org/#elements>
## <https://www.w3.org/Bugs/Public/show_bug.cgi?id=23225>
'class'="definition_type" identifier=_"name" _ClassInheritance '{' InterfaceMembers '}' ';'

* Partial

'partial'=partial? PartialDefinition

* PartialDefinition

PartialInterface
|
PartialDictionary

* PartialInterface

'interface'="definition_type" identifier=_"name" '{' InterfaceMembers '}' ';'

* InterfaceMembers

_InterfaceMember*=members[]

* _InterfaceMember

+pos
ExtendedAttributeList InterfaceMember

* InterfaceMember

Const
|
AttributeOrOperationOrIterator

* Dictionary

'dictionary'="definition_type" identifier=_"name" Inheritance '{' DictionaryMembers '}' ';'

* DictionaryMembers

_DictionaryMember*=members[]

* _DictionaryMember

+pos
+member_type="dictionary_member"
ExtendedAttributeList DictionaryMember

* DictionaryMember

Type identifier=_"name" Default ';'

* PartialDictionary

'dictionary'="definition_type" identifier=_"name" '{' DictionaryMembers '}' ';'

* Default

'=' DefaultValue
?

* DefaultValue

ConstValue
|
string="value_string"

* Exception

'exception'="definition_type" identifier=_"name" Inheritance '{' ExceptionMembers '}' ';'

* ExceptionMembers

_ExceptionMember*=members[]

* _ExceptionMember

+pos
ExtendedAttributeList ExceptionMember

* Inheritance

':' identifier="super_name"
?

* _ClassInheritance

## Not in spec
'extends' identifier="super_name"
?

* Enum

'enum'="definition_type" identifier=_"name" '{' EnumValueList '}' ';'

* EnumValueList

string="value_strings"[] EnumValueListComma

* EnumValueListComma

',' EnumValueListString
?

* EnumValueListString

string="value_strings"[] EnumValueListComma
?

* CallbackRest

identifier=_"name" '=' ReturnType '(' ArgumentList ')' ';'

* Typedef

'typedef'="definition_type" Type identifier=_"name" ';'

* ImplementsStatement

identifier="name" 'implements'="definition_type" identifier="super_name" ';'

* Const

'const'="member_type" ConstType identifier=_"name" '=' ConstValue ';'

* ConstValue

BooleanLiteral
|
FloatLiteral
|
integer="value_integer"
|
'null'="value"

* BooleanLiteral

'true'="value"
|
'false'="value"

* FloatLiteral

float="value_float"
|
'-Infinity'="value"
|
'Infinity'="value"
|
'NaN'="value"

* AttributeOrOperationOrIterator

Serializer
|
Stringifier
|
StaticMember
|
Attribute
|
OperationOrIterator

* Serializer

'serializer'=serializer? SerializerRest

* SerializerRest

## 'ReturnType' is not in spec grammer but required in prose
ReturnType
OperationRest
|
'=' SerializationPattern
|
_SerializerEnd

* _SerializerEnd

+member_type="operation"
+type="DOMString"
+arguments[]
';'

## ';' is missing in spec

* SerializationPattern

+member_type="operation"
+type="DOMString"
+arguments[]

'{' SerializationPatternMap=value_map '}' ';'
|
'[' SerializationPatternList=value_list ']' ';'
|
identifier="value_name" ';'

## ';' is missing in spec

* SerializationPatternMap

+value_names[]

'getter'=getter?
|

## Not in spec grammer but defined in prose
'attribute'=attribute?
|
'inherit'=inherit? ',' 'attribute'=attribute?

|
'inherit'=inherit? Identifiers
|
identifier="value_names"[] Identifiers

?

* SerializationPatternList

+value_names[]

'getter'=getter?
|
identifier="value_names"[] Identifiers
?

* Stringifier

'stringifier'=stringifier? StringifierRest

* StringifierRest

AttributeRest
|
ReturnType OperationRest
|
_StringifierEnd

* _StringifierEnd

+member_type="operation"
+type="DOMString"
+arguments[]
';'

* StaticMember

'static'=static? StaticMemberRest

* StaticMemberRest

AttributeRest
|
ReturnType OperationRest

* Attribute

Inherit AttributeRest

* AttributeRest

ReadOnly 'attribute'="member_type" Type identifier=_"name" ';'

* Inherit

'inherit'=inherit?
?

* ReadOnly

'readonly'=readonly?
?

* OperationOrIterator

ReturnType OperationOrIteratorRest
|
SpecialOperation

* SpecialOperation

Special Specials ReturnType OperationRest

* Specials

Special Specials
?

* Special

'getter'=getter?
|
'setter'=setter?
|
'creator'=creator?
|
'deleter'=deleter?
|
'legacycaller'=legacycaller?

* OperationOrIteratorRest

IteratorRest
|
OperationRest

* IteratorRest

#'iterator'="member_type" OperationIteratorInterfaceOrObject ';'

'iterator'="member_type" OptionalIteratorInterface ';'
|
_IteratorObject

* OperationIteratorInterfaceOrObject

OptionalIteratorInterface
|
'object'=object?

* _IteratorObject

+member_type="iterator_object"
'iterator' 'object' ';'

* OptionalIteratorInterface

'=' identifier="value_name"
?

* OperationRest

+member_type="operation"
OptionalIdentifier '(' ArgumentList ')' ';'

* OptionalIdentifier

identifier=_"name"
?

* ArgumentList

+arguments[]
Argument=arguments[] Arguments
?

* Arguments

',' Argument=arguments[] Arguments
?

* Argument

+pos
ExtendedAttributeList OptionalOrRequiredArgument

* OptionalOrRequiredArgument

'optional'=optional? Type ArgumentName Default
|
Type Ellipsis ArgumentName

* ArgumentName

ArgumentNameKeyword
|
identifier=_"name"

* Ellipsis

'...'=variadic?
?

* ExceptionMember

Const
|
ExceptionField

* ExceptionField

+member_type="field"
Type identifier=_"name" ';'

* ExtendedAttributeList

'[' ExtendedAttribute=extended_attributes[] ExtendedAttributes ']'
?

* ExtendedAttributes

',' ExtendedAttribute=extended_attributes[] ExtendedAttributes
?

* ExtendedAttribute

+pos

#'('="item_type" ExtendedAttributeInner ')' ExtendedAttributeRest
#|
#'['="item_type" ExtendedAttributeInner ']' ExtendedAttributeRest
#|
#'{'="item_type" ExtendedAttributeInner '}' ExtendedAttributeRest
#|
#Other ExtendedAttributeRest

ExtendedAttributeArgList
|
ExtendedAttributeTypePair
|
#ExtendedAttributeIdent
#|
ExtendedAttributeNamedArgList
|
ExtendedAttributeIdentList
|
ExtendedAttributeNoArgs

* ExtendedAttributeRest

ExtendedAttribute=extended_attributes[]
?

* ExtendedAttributeInner

'('="item_type" ExtendedAttributeInner ')' ExtendedAttributeInner
|
'['="item_type" ExtendedAttributeInner ']' ExtendedAttributeInner
|
'{'="item_type" ExtendedAttributeInner '}' ExtendedAttributeInner
|
OtherOrComma ExtendedAttributeInner
?

* Type

SingleType
|
UnionType TypeSuffix

* SingleType

NonAnyType
|
'any'="type" TypeSuffixStartingWithArray

* UnionType

'(' UnionMemberType=type_union[] 'or' UnionMemberType=type_union[] UnionMemberTypes ')'

* UnionMemberType

NonAnyType
|
UnionType TypeSuffix
|
'any'="type" _TypeArray=type_array

* UnionMemberTypes

'or' UnionMemberType=type_union[] UnionMemberTypes
?

* NonAnyType

PrimitiveType TypeSuffix
|
'ByteString'="type" TypeSuffix
|
'DOMString'="type" TypeSuffix
|
identifier="type_name" TypeSuffix
|
_SequenceType=type_parameterized Null
|
'object'="type" TypeSuffix
|
'Date'="type" TypeSuffix
|
'RegExp'="type" TypeSuffix

|
## Not in spec
_PromiseType=type_parameterized Null
|
'Promise'="type" TypeSuffix

* _SequenceType

'sequence'="type_outer" '<' Type '>'

* _PromiseType

'Promise'="type_outer" '<' Type '>'

* ConstType

PrimitiveType Null
|
identifier="type_name" Null

* PrimitiveType

UnsignedIntegerType
|
UnrestrictedFloatType
|
'boolean'="type"
|
'byte'="type"
|
'octet'="type"

* UnrestrictedFloatType

#'unrestricted' FloatType
_UnrestrictedFloat
|
_UnrestrictedDouble

|
FloatType

* FloatType

'float'="type"
|
'double'="type"

* _UnrestrictedFloat

+type="unrestricted float"
'unrestricted' 'float'

* _UnrestrictedDouble

+type="unrestricted double"
'unrestricted' 'double'

* UnsignedIntegerType

#'unsigned' IntegerType
_UnsignedShort
|
_UnsignedLongLong
|
_UnsignedLong

|
IntegerType

* IntegerType

'short'="type"
|
#'long'="type" OptionalLong
_LongLong
|
_Long

* OptionalLong

'long'
?

* _Long

'long'="type"

* _LongLong

+type="long long"
'long' 'long'

* _UnsignedShort

+type="unsigned short"
'unsigned' 'short'

* _UnsignedLong

+type="unsigned long"
'unsigned' 'long'

* _UnsignedLongLong

+type="unsigned long long"
'unsigned' 'long' 'long'

* TypeSuffix

_TypeArray=type_array
|
'?'=type_nullable? TypeSuffixStartingWithArray
?

* TypeSuffixStartingWithArray

_TypeArray=type_array
?

* _TypeArray

'[' ']' TypeSuffix

* Null

'?'=type_nullable?
?

* ReturnType

Type
|
'void'="type"

* IdentifierList

identifier="value_names"[]!'='!'(' Identifiers

* Identifiers

',' identifier="value_names"[]!'='!'(' Identifiers
?

* ExtendedAttributeNoArgs

identifier="name"!'('!'='

* ExtendedAttributeArgList

identifier="name" '(' ArgumentList ')'

* ExtendedAttributeIdent

## Unused (-> ExtendedAttributeIdentList)
identifier="name" '=' identifier="value_name"!'='!'('

* ExtendedAttributeIdentList

identifier="name" '=' IdentifierList!'='!'('

* ExtendedAttributeNamedArgList

identifier="name" '=' identifier="value_names"[] '(' ArgumentList ')'

* ExtendedAttributeTypePair

identifier="name" '(' Type=value_types[] ',' Type=value_types[] ')'
